plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco' // sonarqube
	id "org.sonarqube" version "4.4.1.3373"
}

group = 'clabi'
version = '0.0.1-SNAPSHOT'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'com.jcraft:jsch:0.1.55'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.mapstruct:mapstruct:1.6.0'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.0'

	runtimeOnly 'com.mysql:mysql-connector-j'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// spring security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// flyway
	implementation 'org.flywaydb:flyway-mysql'
	implementation 'org.flywaydb:flyway-core'


    implementation 'me.paulschwarz:spring-dotenv:4.0.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacoco {
	toolVersion = '0.8.11'
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

sonarqube {
	properties {
		property "sonar.projectKey", "auto-wiki"
		property "sonar.host.url", "http://localhost:9000"

		// 커버리지 제외 (핵심)
		property "sonar.coverage.exclusions",
				"**/entity/**," +
						"**/dto/**," +
						"**/config/**," +
						"**/enums/**," +
						"**/client/**"

		// JaCoCo 리포트
		property "sonar.coverage.jacoco.xmlReportPaths",
				"build/reports/jacoco/test/jacocoTestReport.xml"
	}
}